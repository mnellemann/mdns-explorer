plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.15'
    id "com.github.ben-manes.versions" version "0.51.0"
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.google.osdetector' version '1.7.3'
    id 'org.beryx.jlink' version '3.0.1'
}

repositories {
    mavenCentral()
    mavenLocal()
}

tasks.compileJava {
    options.encoding = 'UTF-8'
    options.forkOptions.jvmArgs = [ "--enable-native-access=javafx.graphics" ]
}

application {
    mainModule = 'biz.nellemann.mdexpl'
    mainClass = 'biz.nellemann.mdexpl.App'
    applicationDefaultJvmArgs = [ "-XX:+ExitOnOutOfMemoryError", "-Djava.net.preferIPv4Stack=true",
                                  "--enable-native-access=javafx.graphics" ]
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
    modularity.inferModulePath = false
}

/* This is to be able to build with a JDK not bundled with JavaFX */
javafx {
    version = '25.0.1'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
    // platform("linux-aarch64")
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.17'
    runtimeOnly 'org.slf4j:slf4j-simple:2.0.17'

    implementation 'io.github.mkpaz:atlantafx-base:2.0.1'
    implementation 'jakarta.inject:jakarta.inject-api:2.0.1'
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    implementation 'org.jmdns:jmdns:3.6.3'


    testImplementation 'org.spockframework:spock-core:2.4-groovy-4.0'
    testImplementation 'org.slf4j:slf4j-simple:2.0.17'
}

test {
    useJUnitPlatform()
}

jlink {

    forceMerge 'slf4j'

    options = [
        '--strip-debug',
        '--compress=zip-9',
        '--no-header-files',
        '--no-man-pages'
    ]

    launcher {
        name = 'mDNS-Explorer'
        noConsole = true
    }

    // Only works with Java 14 (and later)
    jpackage {
        imageName = "mDNS-Explorer"
        skipInstaller = true
        installerName = "mDNS-Explorer-${osdetector.arch}"
        installerOptions += [
            '--vendor', 'Mark Nellemann',
            '--description', 'List mDNS services on your local network.',
            '--copyright', 'Mark Nellemann <mark.nellemann@gmail.com>',
            '--app-version', project.findProperty('version')
        ]
        icon = 'src/main/resources/icon.png'

        // Requires: https://wixtoolset.org/ to create installer on Windows
        if(osdetector.os == 'windows') {
            installerType = 'msi'
            skipInstaller = false
            installerOptions += [
                '--win-per-user-install',
                '--win-dir-chooser',
                '--win-menu',
                // '--icon', 'src/main/resources/icon.ico'
            ]
        }

        // Requires: xcode-select --install
        if(osdetector.os == 'osx') {
            installerType = 'dmg'
            skipInstaller = false
            installerOptions += [
                //'--icon', 'src/main/resources/icon.icns'
            ]
        }

        // Requires: build-rpm / rpm and dpkg-dev
        if(osdetector.os == 'linux') {
            skipInstaller = false
            installerOptions += [
                '--linux-shortcut',
                '--linux-menu-group', 'Internet',
                '--linux-rpm-license-type', 'APACHE-20',
                '--linux-deb-maintainer', 'mark.nellemann@gmail.com',
                '--icon', 'src/main/resources/icon.png',
            ]
        }

    }

}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
